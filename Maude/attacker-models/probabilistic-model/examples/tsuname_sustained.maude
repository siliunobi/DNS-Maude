load ../../../src/probabilistic-model/dns
load ../../../src/probabilistic-model/sampler
load ../../../src/probabilistic-model/properties
load ../attacker

--- This is the "NZ event" as described in the tsuNAME paper.
--- The "victims" are the NZ authoritative name servers.

mod TEST is
  inc SAMPLER + APMAUDE + DNS + PROPERTIES + ATTACKER .

  eq configTsuNAMEslistCircDep = true . --- enable TsuNAME-like resolver behavior

  --- turn on name server resource bounds and resolver timeouts
  eq nsResourceBounds? = true .
  eq rsvTimeout? = true .
  eq rsvTimeout = 100.0 .

  --- turn off detailed monitoring for better performance
  eq monitorQueryLog? = false .

  op testTTL : -> Float .
  eq testTTL = 3600.0 . --- Note that this is in time units, not seconds

  ops mAddr cAddr rAddr rAddr1 atkrAddr : -> Address .

  --- "SBELT": fallback if there are no known name servers
  op sb : -> ZoneState .
  eq sb = < root ('a . 'root-servers . 'net . root |-> addrRoot) > .

  ops addrRoot addrNSnz addrNS1a addrNS2a addrNS1b addrNS2b : -> Address .

  ops zoneRoot zoneNZ zoneDomainA zoneDomainB : -> List{Record} .
  eq zoneRoot =
    --- authoritative data
    < root, soa, testTTL, soaData(testTTL) > --- dummy value
    < root, ns, testTTL, 'a . 'root-servers . 'net . root >
    < 'a . 'root-servers . 'net . root, a, testTTL, addrRoot > --- actually, not authoritative, but unimportant for this example

    --- non-authoritative data
    < 'nz . root, ns, testTTL, 'ns . 'nz . root >
    < 'ns . 'nz . root, a, testTTL, addrNSnz > . --- glue

  eq zoneNZ =
    --- authoritative data
    < 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'nz . root, ns, testTTL, 'ns . 'nz . 'root >
    < 'ns . 'nz . root, a, testTTL, addrNSnz >

    --- non-authoritative data
    < 'domainA . 'nz . root, ns, testTTL, 'ns1 . 'domainB . 'nz . root >
    < 'domainA . 'nz . root, ns, testTTL, 'ns2 . 'domainB . 'nz . root > --- in-bailiwick sibling domain, i.e., glue is optional

    < 'domainB . 'nz . root, ns, testTTL, 'ns1 . 'domainA . 'nz . root >
    < 'domainB . 'nz . root, ns, testTTL, 'ns2 . 'domainA . 'nz . root > . --- in-bailiwick sibling domain, i.e., glue is optional

  eq zoneDomainA =
    --- authoritative data
    < 'domainA . 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'domainA . 'nz . root, ns, testTTL, 'ns1 . 'domainB . 'nz . root >
    < 'domainA . 'nz . root, ns, testTTL, 'ns2 . 'domainB . 'nz . root > . --- out-of-bailiwick, i.e., no glue allowed

  eq zoneDomainB =
    --- authoritative data
    < 'domainB . 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'domainB . 'nz . root, ns, testTTL, 'ns1 . 'domainA . 'nz . root >
    < 'domainB . 'nz . root, ns, testTTL, 'ns2 . 'domainA . 'nz . root > . --- out-of-bailiwick, i.e., no glue allowed

  op initConfig : -> Config .
  eq initConfig = run(initState, limit) .

  eq initState = { 0.0 | nil }
    --- Preliminaries
    initMonitor(mAddr)
    [10.0 + atkrInitDelay, to cAddr : start, 0] --- start legitimate client (give attacker head start)

    --- Legitimate client
    < cAddr : Client | queries: genQueries(20, 'nz . root, 1, a, cAddr),
                       resolver: rAddr1,
                       notifyDone: atkrAddr >

    --- Resolver used by the legitimate client
    < rAddr1 : Resolver | cache: nilCache,
                          nxdomainCache: nilNxdomainCache,
                          nodataCache: nilNodataCache,
                          sbelt: sb,
                          workBudget: emptyIN,
                          blockedQueries: eptQSS,
                          sentQueries: eptQSS >

    --- Root name server
    < addrRoot : Nameserver | db: zoneRoot, queue: nilQueue >
    --- .nz authoritative name server
    < addrNSnz : Nameserver | db: zoneNZ, queue: nilQueue >
    --- SLD name servers (these are actually not required; they are never queried)
    < addrNS1a : Nameserver | db: zoneDomainB, queue: nilQueue >
    < addrNS2a : Nameserver | db: zoneDomainB, queue: nilQueue >
    < addrNS1b : Nameserver | db: zoneDomainA, queue: nilQueue >
    < addrNS2b : Nameserver | db: zoneDomainA, queue: nilQueue >
    
    --- Malicious client
    nxdomainClient(atkrAddr, 'domainA . 'nz . root, 1, rAddr, 0.1)

    --- Resolver used by the attacker
    < rAddr : Resolver | cache: nilCache,
                         nxdomainCache: nilNxdomainCache,
                         nodataCache: nilNodataCache,
                         sbelt: sb,
                         workBudget: emptyIN,
                         blockedQueries: eptQSS,
                         sentQueries: eptQSS > .

endm

--- uncomment for debugging purposes
--- set trace on .
set clear rules off .

set trace condition off . --- This does not seem to work
set trace whole off .
set trace substitution off .
set trace mb off .
set trace eq off .
set trace rl on . --- on
set trace select off .
set trace rewrite off .
set trace body off .
set trace builtin off .

--- rew initConfig .

rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
