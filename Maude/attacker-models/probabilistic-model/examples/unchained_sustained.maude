load ../../../src/probabilistic-model/dns
load ../../../src/probabilistic-model/sampler
load ../../../src/probabilistic-model/properties
load ../attacker

--- This is the DNS Unchained attack.

mod TEST is
  inc SAMPLER + APMAUDE + DNS + PROPERTIES + ATTACKER .

  --- turn on name server resource bounds and resolver timeouts
  eq nsResourceBounds? = true .
  eq rsvTimeout? = true .

  --- turn off detailed monitoring for better performance
  eq monitorQueryLog? = false .

  op testTTL : -> Float .
  eq testTTL = 3600.0 . --- Note that this is in time units, not seconds

  ops mAddr cAddr rAddr rAddr1 atkrAddr : -> Address .

  --- "SBELT": fallback if there are no known name servers
  op sb : -> ZoneState .
  eq sb = < root ('a . 'root-servers . 'net . root |-> addrRoot) > .

  op cache : -> Cache .
  eq cache =
    cacheEntry(< 'target-ans . 'com . root, ns, testTTL, 'ns . 'target-ans . 'com . root >, 1)
    cacheEntry(< 'ns . 'target-ans . 'com . root, a, testTTL, addrNStargetANS >, 1)
    cacheEntry(< 'intermediary . 'com . root, ns, testTTL, 'ns . 'intermediary . 'com . root >, 1)
    cacheEntry(< 'ns . 'intermediary . 'com . root, a, testTTL, addrNSintermediary >, 1) .

  ops addrRoot addrNScom addrNStargetANS addrNSintermediary : -> Address .

  ops zoneRoot zoneCom zoneTargetANS zoneIntermediary : -> List{Record} .
  eq zoneRoot =
    --- authoritative data
    < root, soa, testTTL, soaData(testTTL) > --- dummy value
    < root, ns, testTTL, 'a . 'root-servers . 'net . root >

    --- non-authoritative data
    < 'a . 'root-servers . 'net . root, a, testTTL, addrRoot >
    < 'com . root, ns, testTTL, 'ns . 'com . root >
    < 'ns . 'com . root, a, testTTL, addrNScom > . --- glue

  eq zoneCom =
    --- authoritative data
    < 'com . root, soa, testTTL, soaData(testTTL) >
    < 'com . root, ns, testTTL, 'ns . 'com . 'root >
    < 'ns . 'com . root, a, testTTL, addrNScom >

    --- non-authoritative data
    < 'target-ans . 'com . root, ns, testTTL, 'ns . 'target-ans . 'com . root >
    < 'ns . 'target-ans . 'com . root, a, testTTL, addrNStargetANS > --- glue
    < 'intermediary . 'com . root, ns, testTTL, 'ns . 'intermediary . 'com . root >
    < 'ns . 'intermediary . 'com . root, a, testTTL, addrNSintermediary > . --- glue

  eq zoneTargetANS =
    --- authoritative data
    < 'target-ans . 'com . root, soa, testTTL, soaData(testTTL) >
    < 'target-ans . 'com . root, ns, testTTL, 'ns . 'target-ans . 'com . root >
    < 'ns . 'target-ans . 'com . root, a, testTTL, addrNStargetANS >

    --- half of the CNAME chain (every second record); note the TTL of zero
    unchainedRecordsTarget('target-ans . 'com . root, 'intermediary . 'com . root, 10, 0, 0.0) .

  eq zoneIntermediary =
    --- authoritative data
    < 'intermediary . 'com . root, soa, testTTL, soaData(testTTL) >
    < 'intermediary . 'com . root, ns, testTTL, 'ns . 'intermediary . 'com . root >
    < 'ns . 'intermediary . 'com . root, a, testTTL, addrNSintermediary >

    --- the other half of the CNAME chain
    unchainedRecordsIntermediary('target-ans . 'com . root, 'intermediary . 'com . root, 10, 0, testTTL) .

  op initConfig : -> Config .
  eq initConfig = run(initState, limit) .

  eq initState = { 0.0 | nil }
    --- Preliminaries
    initMonitor(mAddr)
    [10.0 + atkrInitDelay, to cAddr : start, 0] --- start legitimate client (give attacker head start)

    --- Legitimate client; queries names for which the target ANS is authoritative
    < cAddr : Client | queries: genQueries(20, 'target-ans . 'com . root, 1, a, cAddr),
                       resolver: rAddr1,
                       notifyDone: atkrAddr >

    --- Resolver used by the legitimate client
    < rAddr1 : Resolver | cache: cache, --- assuming the target-ans.com and intermediary.com name servers are cached
                          nxdomainCache: nilNxdomainCache,
                          nodataCache: nilNodataCache,
                          sbelt: sb,
                          workBudget: emptyIN,
                          blockedQueries: eptQSS,
                          sentQueries: eptQSS >

    --- Root name server
    < addrRoot : Nameserver | db: zoneRoot, queue: nilQueue >
    --- .com authoritative name server
    < addrNScom : Nameserver | db: zoneCom, queue: nilQueue >

    --- target-ans.com authoritative name server (= the victim of the attack)
    < addrNStargetANS : Nameserver | db: zoneTargetANS, queue: nilQueue >
    --- intermediary.com authoritative name server
    < addrNSintermediary : Nameserver | db: zoneIntermediary, queue: nilQueue >

    --- Malicious (repeating) client
    repeatingClient(atkrAddr, rAddr, unchainedQuery('target-ans . 'com . root, 10, 0), 1.0)

    --- Resolver used by the attacker
    < rAddr : Resolver | cache: cache, --- assuming the target-ans.com and intermediary.com name servers are cached
                         nxdomainCache: nilNxdomainCache,
                         nodataCache: nilNodataCache,
                         sbelt: sb,
                         workBudget: emptyIN,
                         blockedQueries: eptQSS,
                         sentQueries: eptQSS > .

endm

--- uncomment for debugging purposes
--- set trace on .
set clear rules off .

set trace condition off . --- This does not seem to work
set trace whole off .
set trace substitution off .
set trace mb off .
set trace eq off .
set trace rl on . --- on
set trace select off .
set trace rewrite off .
set trace body off .
set trace builtin off .

--- rew initConfig .

rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
rew clientQSuccessRatio(initConfig) .
