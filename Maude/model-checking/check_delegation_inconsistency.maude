load preds
load ../test/nondet-model/test_helpers

--- In this example, there are inconsistencies in the delegation between the parent zone (com) and child zone (example.com).

mod DNS-CHECK is
  pr DNS-PREDS .
  inc MODEL-CHECKER .
  inc LTL-SIMPLIFIER .

  inc TEST-HELPERS .

  var QS : List{Query} .
  var CACHE : Cache .

  op testTTL : -> Float .
  eq testTTL = 3600.0 . --- Note that this is in time units, not seconds

  ops q qGlue qNS : -> Query .
  eq q = query(1, 'www . 'example . 'com . root, a) .
  eq qGlue = query(2, 'ns2 . 'example . 'com . root, a) .
  eq qNS = query(2, 'example . 'com . root, ns) .

  ops mAddr cAddr rAddr : -> Address .

  op sb : -> ZoneState . --- "SBELT": fallback if no known name servers
  eq sb = < root ('a . 'root-servers . 'net . root |-> addrRoot) > .

  ops addrRoot addrNS1com addrNS2com addrNS1example addrNS2example : -> Address .

  ops dbRoot dbNScom dbNSexample dbNSexample' : -> List{Record} .

  eq dbRoot =
    --- authoritative data
    < root, soa, testTTL, soaData(testTTL) > --- dummy value
    < root, ns, testTTL, 'a . 'root-servers . 'net . root >

    --- non-authoritative data
    < 'com . root, ns, testTTL, 'ns1 . 'com . root >
    < 'com . root, ns, testTTL, 'ns2 . 'com . root >
    < 'ns1 . 'com . root, a, testTTL, addrNS1com >   --- glue record
    < 'ns2 . 'com . root, a, testTTL, addrNS2com > . --- glue record

  eq dbNScom =
    --- authoritative data
    < 'com . root, soa, testTTL, soaData(testTTL) > --- dummy value
    < 'com . root, ns, testTTL, 'ns1 . 'com . root >
    < 'com . root, ns, testTTL, 'ns2 . 'com . root >
    < 'ns1 . 'com . root, a, testTTL, addrNS1com >
    < 'ns2 . 'com . root, a, testTTL, addrNS2com >

    --- non-authoritative data
    < 'example . 'com . root, ns, testTTL, 'ns1 . 'example . 'com . root >
    < 'ns1 . 'example . 'com . root, a, testTTL, addrNS1example > --- glue record
    < 'example . 'com . root, ns, testTTL, 'ns2 . 'example . 'com . root >
    < 'ns2 . 'example . 'com . root, a, testTTL, addrNS2example > . --- glue record

  eq dbNSexample =
    --- authoritative data
    < 'example . 'com . root, soa, testTTL, soaData(testTTL) >
    < 'example . 'com . root, ns, testTTL, 'ns1 . 'example . 'com . root >
    < 'example . 'com . root, ns, testTTL, 'ns2 . 'example . 'com . root >

    < 'ns1 . 'example . 'com . root, a, testTTL, addrNS1example >
    < 'ns2 . 'example . 'com . root, a, testTTL, addrNS2example >
    < 'www . 'example . 'com . root, a, testTTL, 1 . 1 . 1 . 1 > .

  eq dbNSexample' =
    --- authoritative data
    < 'example . 'com . root, soa, testTTL, soaData(testTTL) >
    < 'example . 'com . root, ns, testTTL, 'nsXXX . 'example . 'com . root > --- Note the inconsistent NS record!
    < 'example . 'com . root, ns, testTTL, 'ns2 . 'example . 'com . root >

    < 'ns1 . 'example . 'com . root, a, testTTL, addrNS1example >
    < 'ns2 . 'example . 'com . root, a, testTTL, addrNS1example > --- Note the inconsistent address!
    < 'www . 'example . 'com . root, a, testTTL, 1 . 1 . 1 . 1 > .

  op initConfig : List{Query} Cache -> Config .
  eq initConfig(QS, CACHE) =
    --- Preliminaries
    initMonitor(mAddr)
    (to cAddr : start)

    < cAddr : Client | queries: QS, resolver: rAddr, notifyDone: nullAddr >
    < rAddr : Resolver | cache: CACHE,
                         nxdomainCache: nilNxdomainCache,
                         nodataCache: nilNodataCache,
                         sbelt: sb,
                         workBudget: emptyIN,
                         blockedQueries: eptQSS,
                         sentQueries: eptQSS >

    --- Name servers
    < addrRoot : Nameserver | db: dbRoot, queue: nilQueue >
    < addrNS1com : Nameserver | db: dbNScom, queue: nilQueue >
    < addrNS2com : Nameserver | db: dbNScom, queue: nilQueue >
    < addrNS1example : Nameserver | db: dbNSexample, queue: nilQueue >
    < addrNS2example : Nameserver | db: dbNSexample', queue: nilQueue > .

endm

rew initConfig(q, nilCache) .

--- If we query for www.example.com, we do not discover the inconsistent delegation.
search [1] initConfig(q, nilCache) =>* C:Config such that hasDelegationInconsistency(C:Config) .

--- But if we query for the address of ns2.example.com, we do discover the inconsistent glue.
search [1] initConfig(qGlue, nilCache) =>* C:Config such that hasDelegationInconsistency(C:Config) .

--- And if we query for the NS records of example.com, we discover the inconsistent delegation.
search [1] initConfig(qNS, nilCache) =>* C:Config such that hasDelegationInconsistency(C:Config) .

red modelCheck(initConfig(qNS, nilCache), [] ~ propDelegationInconsistency) .

--- The fact that there are inconsistent zones is also easily detected statically
--- but this does not necessarily mean that we can actually obtain inconsistent answers
red hasInconsistentRRsets(initConfig(q, nilCache)) .
