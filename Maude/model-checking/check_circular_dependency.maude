load preds
load ../test/nondet-model/test_helpers

--- In this example, there is a circular dependency between the nameservers for domainA.nz and domainB.nz.
--- Since the nameservers are in-bailiwick but under a sibling domain, glue is optional.
--- One of the nz nameservers provides glue, in which case resolution works fine. The other nz nameserver does
--- not provide glue. Whether the resolver encounters the circular dependency thus depends on which nameserver it asks.

mod DNS-CHECK is
  pr DNS-PREDS .
  inc MODEL-CHECKER .
  inc LTL-SIMPLIFIER .

  inc TEST-HELPERS .

  eq monitorSlistArgsLog? = true . --- required for the property

  var QS : List{Query} .
  var CACHE : Cache .

  op testTTL : -> Float .
  eq testTTL = 3600.0 . --- Note that this is in time units, not seconds

  ops q q' : -> Query .
  eq q = query(1, 'www . 'domainA . 'nz . root, a) .
  eq q' = query(2, 'www . 'nz . root, a) .

  ops mAddr cAddr rAddr : -> Address .

  op sb : -> ZoneState . --- "SBELT": fallback if no known name servers
  eq sb = < root ('a . 'root-servers . 'net . root |-> addrRoot) > .

  ops addrRoot addrNS1nz addrNS2nz addrNSa addrNSb : -> Address .

  ops dbRoot dbNZ dbNZnoglue dbDomainA dbDomainB : -> List{Record} .
  eq dbRoot =
    --- authoritative data
    < root, soa, testTTL, soaData(testTTL) > --- dummy value
    < root, ns, testTTL, 'a . 'root-servers . 'net . root >
    < 'a . 'root-servers . 'net . root, a, testTTL, addrRoot > --- actually, not authoritative, but unimportant for this example

    --- non-authoritative data
    < 'nz . root, ns, testTTL, 'ns1 . 'nz . root >
    < 'nz . root, ns, testTTL, 'ns2 . 'nz . root >
    < 'ns1 . 'nz . root, a, testTTL, addrNS1nz > --- glue
    < 'ns2 . 'nz . root, a, testTTL, addrNS2nz > . --- glue

  eq dbNZ =
    --- authoritative data
    < 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'nz . root, ns, testTTL, 'ns1 . 'nz . root >
    < 'nz . root, ns, testTTL, 'ns2 . 'nz . root >
    < 'ns1 . 'nz . root, a, testTTL, addrNS1nz > --- glue
    < 'ns2 . 'nz . root, a, testTTL, addrNS2nz > --- glue

    --- non-authoritative data
    < 'domainA . 'nz . root, ns, testTTL, 'ns . 'domainB . 'nz . root > --- in-bailiwick sibling domain, i.e., glue is optional
    < 'ns . 'domainB . 'nz . root, a, testTTL, addrNSa > --- glue

    < 'domainB . 'nz . root, ns, testTTL, 'ns . 'domainA . 'nz . root > --- in-bailiwick sibling domain, i.e., glue is optional
    < 'ns . 'domainA . 'nz . root, a, testTTL, addrNSa > --- glue
    .

  eq dbNZnoglue =
    --- authoritative data
    < 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'nz . root, ns, testTTL, 'ns1 . 'nz . root >
    < 'nz . root, ns, testTTL, 'ns2 . 'nz . root >
    < 'ns1 . 'nz . root, a, testTTL, addrNS1nz > --- glue
    < 'ns2 . 'nz . root, a, testTTL, addrNS2nz > --- glue

    --- non-authoritative data
    < 'domainA . 'nz . root, ns, testTTL, 'ns . 'domainB . 'nz . root > --- in-bailiwick sibling domain, i.e., glue is optional

    < 'domainB . 'nz . root, ns, testTTL, 'ns . 'domainA . 'nz . root > --- in-bailiwick sibling domain, i.e., glue is optional
    .

  eq dbDomainA =
    --- authoritative data
    < 'domainA . 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'domainA . 'nz . root, ns, testTTL, 'ns . 'domainB . 'nz . root > . --- out-of-bailiwick, i.e., no glue allowed

  eq dbDomainB =
    --- authoritative data
    < 'domainB . 'nz . root, soa, testTTL, soaData(testTTL) >
    < 'domainB . 'nz . root, ns, testTTL, 'ns . 'domainA . 'nz . root > . --- out-of-bailiwick, i.e., no glue allowed

  op initConfig : List{Query} Cache -> Config .
  eq initConfig(QS, CACHE) =
    --- Preliminaries
    initMonitor(mAddr)
    (to cAddr : start)

    < cAddr : Client | queries: QS, resolver: rAddr, notifyDone: nullAddr >
    < rAddr : Resolver | cache: CACHE,
                         nxdomainCache: nilNxdomainCache,
                         nodataCache: nilNodataCache,
                         sbelt: sb,
                         workBudget: emptyIN,
                         blockedQueries: eptQSS,
                         sentQueries: eptQSS >

    --- Name servers
    < addrRoot : Nameserver | db: dbRoot, queue: nilQueue >
    < addrNS1nz : Nameserver | db: dbNZ, queue: nilQueue >
    < addrNS2nz : Nameserver | db: dbNZnoglue, queue: nilQueue >
    < addrNSa : Nameserver | db: dbDomainA, queue: nilQueue >
    < addrNSb : Nameserver | db: dbDomainB, queue: nilQueue > .

endm

rew initConfig(q, nilCache) .

--- There is no way to encounter a circular dependency for www.nz.
search [1] initConfig(q', nilCache) =>* C:Config such that hasCircularDependency(C:Config) .

--- There is an execution for www.domainA.nz reaching a final state without encountering the circular dependency.
search [1] initConfig(q, nilCache) =>! C:Config such that not(hasCircularDependency(C:Config)) and reachedTerminalConfig(C:Config) .

--- There is an execution that encounters the circular dependency.
search [1] initConfig(q, nilCache) =>* C:Config such that hasCircularDependency(C:Config) .

red modelCheck(initConfig(q, nilCache), [] ~ propCircularDependency) .
